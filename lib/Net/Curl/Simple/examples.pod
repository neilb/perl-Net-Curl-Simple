=head1 NAME

Net::Curl::Simple::examples - sample modules for Net::Curl::Simple

=head1 Irssi CPAN search

=head4 I<Extracted from C<examples/irssi-cpan-search.pl>>

This example searches modules on CPAN.

 # irssi script

 use strict;
 use warnings;
 use Irssi;
 use IO::File;
 use URI::Escape;
 use Net::Curl::Simple;

 sub got_body
 {
     my ( $window, $easy ) = @_;
     if ( my $result = $easy->code ) {
         warn "Could not download $easy->{uri}: $result\n";
         return;
     }

     my @found;
     while ( $easy->{body} =~ s#<h2 class=sr><a href="(.*?)"><b>(.*?)</b></a></h2>## ) {
         my $uri = $1;
         $_ = $2;
         s/&#(\d+);/chr $1/eg;
         chomp;
         push @found, $_;
     }
     @found = "no results" unless @found;
     my $msg = "CPAN search %9$easy->{args}%n: " . (join "%9;%n ", @found);
     if ( $window ) {
         $window->print( $msg );
     } else {
         Irssi::print( $msg );
     }
 }

 sub cpan_search
 {
     my ( $args, $server, $window ) = @_;

     my $query = uri_escape( $args );
     my $uri = "http://search.cpan.org/search?query=${query}&mode=all";
     my $easy = Net::Curl::Simple->new();
     $easy->{args} = $args;
     $easy->get( $uri, sub { got_body( $window, @_ ) } );
 }

 Irssi::command_bind( 'cpan', \&cpan_search );

 1;

 # vim: ts=4:sw=4

=cut
